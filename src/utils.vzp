/* TODO use isDigit to check each digit first! */
let atoi = (string :: [Int64]) =>
    ((x :: Int64, y :: Int64) => ((x * 10) + (y - 48))) <@ 0 string

let itoa = (number :: Int64) =>
    (if number >= 10 then recur(number / 10) else "") + [(number % 10) + 48]

/* TODO inefficient TODO types */
let in = (x :: [Int64], collection :: [[Int64]]) =>
    || @> ((y :: [Int64]) => x == y) $ collection false

let join = (alos :: [[Int64]]) =>
    ((x :: [Int64], y :: [Int64]) =>
        (if y == "" then x else x + ", " + y)) @> alos ""


let isDigit = (c :: Int64) => c <= 57 && c >= 48

let isAlpha = (c :: Int64) => (c <= 90 && c >= 65) || (c <= 122 && c >= 97)

let isAlnum = (c :: Int64) => isDigit(c) || isAlpha(c)

let lastCharIndexHelper = (string :: [Int64], c :: Int64, index :: Int64) =>
    if index < 0 || string:index == c then index else recur(string, c, index-1)

let lastCharIndex = (string :: [Int64], c :: Int64) =>
    lastCharIndexHelper(string, c, string.length-1)

let strIndexInArray = (str :: [Int64], array :: [[Int64]]) =>
    if array == [] then -1 else
    if array:0 == str then 0 else
    1 + recur(str, if array.length == 1 then [] else array:1..array.length)

{
    atoi: atoi,
    itoa: itoa,
    in: in,
    isAlnum: isAlnum,
    isDigit: isDigit,
    join: join,
    lastCharIndex: lastCharIndex,
    strIndexInArray: strIndexInArray
}
