import defunc
import utils

let escape = (id :: [Int64]) =>
    if utils.in(id, ["true", "false"]) then id else
    let f = (c :: Int64) => 
        if utils.isAlnum(c) then [c] else "_" + utils.itoa(c) + "_"
    "_" + ((+) <@ [] f $ id)

let compile_body_js = (ast :: Object, isBound :: Boolean) =>
    let recTrue = let rf = recur; (x :: Object) => rf(x, true)
    let recFalse = let rf = recur; (x :: Object) => rf(x, false)

    let wrap = (x :: [Int64]) => if isBound then x else "return " + x + ";"

    /* Main block */
    if ast.type == "array" then
        wrap("[" + utils.join(recTrue $ ast.contents) + "]")
    else if ast.type == "call" then
        wrap(recTrue(ast.func) + "(" + utils.join(recTrue $ ast.args) + ")")
    else if ast.type == "field_access" then
        if ast.field == "length" then /* TODO this will not work long term */
            wrap(recTrue(ast.obj) + ".length")
        else
            wrap(recTrue(ast.obj) + "." + escape(ast.field))
    else if ast.type == "js-conditional" then
        "if(" + recTrue(ast.cond) + "){" + recFalse(ast.thn) + "}else{"
                                         + recFalse(ast.els) + "}"
    else if ast.type == "js-assignment" then
        "let " + escape(ast.identifier) + "=" + recTrue(ast.value) + ";" 
                                                + recFalse(ast.body)
    else if ast.type == "import" then
        wrap("require('./" + ast.module + ".js').vzpModule")
    else if ast.type == "lookup" then
        wrap(escape(ast.identifier))
    else if ast.type == "number" then
        wrap(utils.itoa(ast.value))
    else if ast.type == "object" then
        let f = (elem :: Object) => 
            escape(elem.identifier) + ":" + recTrue(elem.value)
        wrap("{" + utils.join(f $ ast.contents) + "}")
    else
        "UNKNOWN" + ast.type

let compile_func_js = (func :: Object) =>
    "function " + escape(func.name) + "(" + utils.join(escape $ func.params)
                + "){" + compile_body_js(func.body, false) + "}"

(program_as_string :: [Int64]) =>
    let prog = defunc(program_as_string)
    "module.exports={vzpModule:(function(){"
  + (+ @> (compile_func_js $ prog.funcs) compile_body_js(prog.ast, false))
  + "})()};" /* TODO various args and such */ 
