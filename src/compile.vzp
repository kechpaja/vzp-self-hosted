import optimize
import utils

let escapeId = (id) =>
    let f = (c) => if utils.isAlnum(c) then [c] else "_" + utils.itoa(c) + "_"
    (+) <@ [] f $ id

let compile_js = (ast, isBound) =>
    let recTrue = let recFunc = recur; (x) => recFunc(x, true)
    let recFalse = let recFunc = recur; (x) => recFunc(x, false)

    /* Main block */
    let strExpr = if ast.type == "array" then
        "[" + utils.join(recTrue $ ast.contents) + "]"
    else if ast.type == "call" then
        recur(ast.func, true) + "(" + utils.join(recTrue $ ast.args) + ")"
    else if ast.type == "conditional" then
        "(" + recTrue(ast.cond) + "?" + recTrue(ast.thn)
                                + ":" + recTrue(ast.els) + ")"
    else if ast.type == "field_access" then
        recTrue(ast.obj) + "." + escapeId(ast.field)
    else if ast.type == "import" then
        "require(./'" + ast.module + ".js').vzpModule"
    else if ast.type == "lambda" then
        "(function(" + utils.join(escapeId $ ast.params) + "){" 
                     + recFalse(ast.body) + "})"
    else if ast.type == "lookup" then
        escapeId(ast.identifier)
    else if ast.type == "number" then
        utils.itoa(ast.value)
    else if ast.type == "object" then
        let f = (elem) => escapeId(elem.identifier) + ":" + recTrue(elem.value)
        "{" + utils.join(f $ ast.contents) + "}"
    else
        "UNKNOWN" + ast.type

    if isBound then strExpr else "return " + strExpr + ";"

(program_as_string) =>
    "module.exports={vzpModule:(function(){"
  + compile_js(optimize(program_as_string), false)
  + "})()};" /* TODO various args and such */ 
